plugins {
    id 'com.github.triplet.play' version '3.7.0'
}

apply plugin: 'com.android.application'

def isCi = System.getenv("CI") == "true"
android {
    compileSdkVersion 32
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "com.gianlu.timeless"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 69
        versionName "2.4.6"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        warning 'MissingTranslation', 'Instantiatable'
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation(project(':CommonUtils')) {
        exclude group: 'com.github.faruktoptas', module: 'FancyShowCaseView'
    }

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.github.scribejava:scribejava-core:8.3.1'
    implementation 'com.github.scribejava:scribejava-httpclient-okhttp:8.3.1'

    implementation 'com.google.android.material:material:1.6.1'

    standardImplementation 'com.google.firebase:firebase-perf:20.1.0'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'