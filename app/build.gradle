plugins {
    id 'com.github.triplet.play' version '2.3.0'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.timeless"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 56
        versionName "2.1.2"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.travis/keystore.jks")
                storePassword System.getenv("keystore_storePsswd")
                keyAlias = System.getenv("keystore_keyAlias")
                keyPassword System.getenv("keystore_keyPsswd")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    defaultToAppBundles = true
    enabled = isTravis
    serviceAccountCredentials = file("../.travis/secrets.json")
    track = "production"
    releaseStatus = "draft"
}

dependencies {
    api(project(':CommonUtils')) {
        exclude group: 'me.toptas.fancyshowcase'
    }

    api 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    api 'com.squareup.okhttp3:okhttp:4.2.2'
    api 'com.github.scribejava:scribejava-core:6.8.0'
    api 'com.github.scribejava:scribejava-httpclient-okhttp:6.8.0'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'