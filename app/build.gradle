plugins {
    id 'com.github.triplet.play' version '3.0.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.timeless"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 61
        versionName "2.3.1"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isTravis)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation(project(':CommonUtils')) {
        exclude group: 'me.toptas.fancyshowcase'
    }

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.github.scribejava:scribejava-core:7.1.1'
    implementation 'com.github.scribejava:scribejava-httpclient-okhttp:7.1.1'

    implementation 'com.google.android.material:material:1.3.0-rc01'

    standardImplementation 'com.google.firebase:firebase-perf:19.1.0'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'